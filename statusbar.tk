#!/usr/bin/env wish9.0
package require utils
package require maildir
package require metar
package require SysInfo
package require MPD

namespace eval statusbar {
	variable xscreen 0
	variable wmport 9981
	variable themefile "$::env(HOME)/.cache/theme/default"

	# Unicode icons from remixicon
	array set remix [ list	down 	\uea4a	up 	\uea74 \
				disk	\ueba9	printer	\uefd2 \
				sdcard	\uf066	udisk	\uf1cc \
				mail	\ueec0	mixer	\uf229 ]

	# List of fixed items in the bar
	set baritems [ list mode modesep desktop desksep loadavg usedmem acpitemp \
			netin netout infosep windowname datetime \
			datesep metar metarsep mixer mixersep ]

	# Array describing items in the bar
	array set widgetarray [ list \
		mode [ list type var side left text statusbar::mode font normal ] \
		modesep [ list type vseparator side left ] \
		desktop [ list type var side left text statusbar::desktops font normal ] \
		desksep [ list type vseparator side left ] \
		loadavg [ list type var side left text statusbar::loadavg font normal ] \
		usedmem [ list type var side left text statusbar::usedmem font normal ] \
		acpitemp [ list type var side left text statusbar::acpitemp font normal ] \
		netin [ list type var side left text statusbar::netin font normal ] \
		netout [ list type var side left text statusbar::netout font normal ] \
		infosep [ list type vseparator side left ] \
		windowname [ list type xvar side left text statusbar::windowname font normal ] \
		datetime [ list type var side right text statusbar::datetime font normal ] \
		datesep [ list type vseparator side right ] \
		metar [ list type var side right text metar::report(statusbar) font normal ] \
		metarsep [ list type vseparator side right ] \
		mixer [ list type var side right text statusbar::mixer font normal ] \
		mixersep [ list type vseparator side right ] ]

	proc dark {} {
		set theme [ list \
			acpitemp	PaleGreen3 \
			bar		{#3b4252} \
			datetime	WhiteSmoke \
			desktop		PaleGreen3 \
			devices		SteelBlue2 \
			loadavg		CadetBlue3 \
			metar		Gold \
			mixer		CadetBlue3 \
			mode		CadetBlue3 \
			musicpd		CadetBlue3 \
			netin		Gold2 \
			netout		CadetBlue3 \
			separator	OliveDrab \
			usedmem		CadetBlue3 \
			windowname	PaleGreen3 ]
	}

	proc light {} {
		set theme [ list \
			acpitemp	MediumBlue \
			bar		gray90 \
			datetime	black \
			desktop		DarkBlue \
			devices		DarkGreen \
			loadavg		black \
			metar		DarkGreen \
			mixer		black \
			mode		DarkBlue \
			musicpd		DarkBlue \
			netin		DarkGreen \
			netout		purple \
			separator	black \
			usedmem		black \
			windowname	black ]
	}

	# Setup the fixed items in the bar
	proc setup_bar {} {
		variable baritems
		variable widgetarray

		frame .bar
		pack .bar -fill both -expand 1
		foreach item $baritems {
			array set widget $widgetarray($item)
			switch $widget(type) {
				label {
					pack [label .bar.$item -font $widget(font) \
						-text $widget(text)] -side $widget(side)
				}
				var {
					pack [label .bar.$item -font $widget(font) \
						-textvar $widget(text)] -side $widget(side)
				}
				xvar {
					pack [label .bar.$item -font $widget(font) \
						-textvar $widget(text)] -side $widget(side) \
						-expand 1
				}
				vseparator {
					pack [frame .bar.$item -width 1] -fill y -padx 5 \
						-side $widget(side)
				}
			}
		}

		bind .bar.mode <1> { statusbar::wm_message {PrevMode} }
		bind .bar.mode <3> { statusbar::wm_message {NextMode} }
		bind .bar.desktop <MouseWheel> {
			if {%D < 0} {
				statusbar::wm_message {PrevDesktop}
			} else {
				statusbar::wm_message {NextDesktop}
			}
		}
		bind .bar.loadavg <1> { exec xterm +sb -class top -e top & }
		bind .bar.mixer <MouseWheel> {
			if {%D < 0} {
				exec mixer vol=-0.05
			} else {
				exec mixer vol=+0.05
			}
			statusbar::update_mixer
		}
		bind .bar.metar <1> { metar::toggle_window }
		bind .bar.metar <2> { metar::update_report }
	}

	# Setup the removable devices
	proc setup_devices {} {
		variable devicelist
		variable devicearray
		variable remix

		frame .bar.devices
		frame .bar.devicessep -width 1

		set devicelist [ list ada1 da0 da1 mmcsd0 ulpt0 ]
		set deviceicons [ list $remix(disk) $remix(udisk) $remix(udisk) \
				$remix(sdcard) $remix(printer) ]

		array set devicearray {}
		foreach devid $devicelist icon $deviceicons {
			array set device {}
			set device(path) "/dev/$devid"
			set device(visible) 0
			set devicearray($devid) [array get device]

			label .bar.devices.$devid
			.bar.devices.$devid configure -font normal -text "$icon $devid"
		}
	}

	# Check the presence of removable devices.
	proc update_devices {} {
		variable devicelist
		variable devicearray

		foreach devid $devicelist {
			array set device $devicearray($devid)
			if {[file exists $device(path)]} {
				if {!$device(visible)} {
					if {![string length [pack slaves .bar.devices]]} {
						pack .bar.devices -after .bar.infosep -side left
						pack .bar.devicessep -after .bar.devices -fill y \
							-padx 5 -side left
					}
					pack .bar.devices.$devid -in .bar.devices -side left
					set device(visible) 1
					set devicearray($devid) [array get device]
				}
			} else {
				if {$device(visible)} {
					pack forget .bar.devices.$devid
					if {![string length [pack slaves .bar.devices]]} {
						pack forget .bar.devices .bar.devicessep
					}
					set device(visible) 0
					set devicearray($devid) [array get device]
				}
			}
		}

	}

	# Setup widget for MPD
	proc setup_musicpd {} {
		variable musicpd_visible
		set musicpd_visible 0

		frame .bar.musicsep -width 1
		label .bar.musicpd -font italic -textvar statusbar::musicpd

		bind .bar.musicpd <1> {
			if {![catch {exec mpc toggle}]} { statusbar::update_musicpd }
		}
		bind .bar.musicpd <2> {
			exec xterm +sb -class ncmpcpp -e ncmpcpp &
		}
		bind .bar.musicpd <3> {
			if {![catch {exec mpc stop}]} { statusbar::update_musicpd }
		}
		bind .bar.musicpd <MouseWheel> {
			if {%D < 0} {
				if {![catch {exec mpc next}]} { statusbar::update_musicpd }
			} else {
				if {![catch {exec mpc prev}]} { statusbar::update_musicpd }
			}

		}
	}

	# Show what is currently playing on MPD
	proc update_musicpd {} {
		variable musicpd
		variable musicpd_visible

		set title [join [musicpd::currenttitle]]
		if {[string length $title]} {
			set musicpd $title
			if {!$musicpd_visible} {
				pack .bar.musicpd -after .bar.mixersep -side right
				pack .bar.musicsep -after .bar.musicpd -fill y -padx 5 -side right
				set musicpd_visible 1
			}

		} else {
			if {$musicpd_visible} {
				pack forget .bar.musicpd .bar.musicsep
				set musicpd_visible 0
				set musicpd ""
			}
		}
	}

	# Set the theme on the alements of the bar
	proc set_theme { theme } {
		variable currenttheme
		variable baritems
		variable widgetarray
		variable devicelist
		variable accountlist

		switch $theme {
			light {
				set currenttheme light
				array set color [light]
			}
			dark {
				set currenttheme dark
				array set color [dark]
			}
			default {
				return
			}
		}

		.bar configure -background $color(bar)
		foreach item $baritems {
			array set widget $widgetarray($item)
			switch $widget(type) {
				vseparator {
					.bar.$item configure -background $color(separator)
				}
				default {
					.bar.$item configure -bg $color(bar) -fg $color($item)
				}
			}
		}

		.bar.devices configure -background $color(bar)
		.bar.devicessep configure -background $color(separator)
		foreach device $devicelist {
			.bar.devices.$device configure -bg $color(bar) -fg $color(devices)
		}

		maildir::set_theme $theme

		.bar.musicpd configure -bg $color(bar) -fg $color(musicpd)
		.bar.musicsep configure -background $color(separator)
	}

	# Send a message to the window manager
	proc wm_message { wmmessage } {
		variable xscreen
		variable wmport

		if {[catch {set channel [socket localhost $wmport]} ]} {
			puts stderr "Could not open socket on port $wmport!\n"
			return
		}
		puts $channel "$xscreen;$wmmessage"
		close $channel
	}

	# Update mixer after button event
	proc update_mixer {} {
		variable mixer
		variable remix
		set mixer "$remix(mixer) [sysinfo::getmixervol]"
	}

	proc bar_update {} {
		variable remix
		variable loadavg
		variable usedmem
		variable acpitemp
		variable netin
		variable netout
		variable datetime
		variable mixer

		set loadavg "[sysinfo::getloadavg] "
		set usedmem "[join [sysinfo::getusedmemswap]] "
		set acpitemp "T [sysinfo::getacpitemp]"
		set netstats [sysinfo::getnetstats em0]
		set netin "$remix(down)[join [lindex $netstats 0]]"
		set netout "$remix(up)[join [lindex $netstats 1]]"

		set datetime [clock format [clock seconds] \
			-locale $::env(LANG) -format {%d %b %H:%M }]

		set mixer "$remix(mixer) [sysinfo::getmixervol]"

		update_devices
		maildir::update_mailboxes
		update_musicpd
	}

	proc read_socket {channel} {
		variable currenttheme
		variable desktops
		variable mode
		variable windowname
		variable metar

		if {[gets $channel line] < 0} {
			close $channel
			return
		}
		close $channel

		set line [string trimright $line]
		set equal [string first "=" $line]
		if {$equal == -1} {
			set key $line
			set value ""
		} elseif {$equal > 0} {
			set key [string range $line 0 $equal-1]
			set value [string range $line $equal+1 $equal+128]
		} else {
			return
		}
		if {$key == "window_active"} {
			set windowname $value
		} elseif {$key == "no_window_active"} {
			set windowname ""
		} elseif {$key == "desktop_list"} {
			set desktops $value
		} elseif {$key == "desktop_mode"} {
			set mode " $value"
		} elseif {$key == "mixer_volume"} {
			statusbar::update_mixer
		} elseif { $key == "toggle_metar_window" } {
			metar::toggle_window
		} elseif { $key == "update_metar_report" } {
			metar::update_report
		} elseif {$key == "set_theme"} {
			if {$value != $currenttheme} {
				set_theme $value
				metar::configure_window $value
			}
		}
	}

	proc server_accept {channel host port} {
		fconfigure $channel -buffering none -blocking 0
		fileevent $channel readable [list statusbar::read_socket $channel]
	}

	proc init {} {
		variable themefile

		setup_bar
		setup_devices
		setup_musicpd
		maildir::init .bar metarsep right

		if {[file exists $themefile]} {
			set_theme [string trim [utils::read_file $themefile]]
		} else {
			set_theme light
		}

		musicpd::connect $::env(MPD_HOST)

		utils::every 2000 statusbar::bar_update
	}
}

set lang fr
set timezone :America/Montreal

tk appname "statusbar"
font create normal -family NotoSans -size 11
font create italic -family NotoSans -size 11 -slant italic
font create bold -family NotoSans -size 11 -weight bold

wm attributes . -topmost 1 -type dock
wm geometry . "[winfo vrootwidth .]x26+0+0"
wm title . "statusbar.tk version 20241017"

statusbar::init
metar::init

socket -server statusbar::server_accept -myaddr localhost 9995
vwait forever
