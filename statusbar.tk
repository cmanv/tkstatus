#!/usr/bin/env wish9.0
package require utils
package require devices
package require maildir
package require metar
package require musicpd
package require SysInfo

namespace eval statusbar {
	variable xscreen 0
	variable wmport 9981
	variable themefile "$::env(XDG_STATE_HOME)/theme/current"
	variable currenttheme light
	variable wintitle

	# Unicode codes from remixicon
	array set icons [ list	down 	\uea4a \
				up 	\uea74 \
				mixer	\uf229 ]

	# List of static items in the statusbar
	set baritems [ list mode modesep desktop desksep loadavg usedmem \
			netin netout infosep title datetime \
			datesep metar metarsep mixer mixersep ]

	# Array describing the static items in the bar
	array set widgetarray [ list \
		mode [ list type var side left text statusbar::mode font normal ] \
		modesep [ list type vseparator side left ] \
		desktop [ list type var side left text statusbar::desktops font normal ] \
		desksep [ list type vseparator side left ] \
		loadavg [ list type var side left text statusbar::loadavg font normal ] \
		usedmem [ list type var side left text statusbar::usedmem font normal ] \
		netin [ list type var side left text statusbar::netin font normal ] \
		netout [ list type var side left text statusbar::netout font normal ] \
		infosep [ list type vseparator side left ] \
		title [ list type text side left height 1 font normal ] \
		datetime [ list type var side right text statusbar::datetime font normal ] \
		datesep [ list type vseparator side right ] \
		metar [ list type var side right text metar::report(statusbar) font normal ] \
		metarsep [ list type vseparator side right ] \
		mixer [ list type var side right text statusbar::mixer font normal ] \
		mixersep [ list type vseparator side right ] ]

	proc dark {} {
		set theme [ list \
			background	{#3b4252} \
			datetime	LightGray \
			desktop		LightGray \
			loadavg		PaleGreen3 \
			metar		Gold \
			mixer		PaleGreen3 \
			mode		LightGray \
			netin		Gold \
			netout		CadetBlue3 \
			separator	Gray \
			usedmem		PaleGreen3 \
			title		LightGray ]
	}

	proc light {} {
		set theme [ list \
			background	gray90 \
			datetime	black \
			desktop		DarkBlue \
			loadavg		black \
			metar		DarkGreen \
			mixer		black \
			mode		DarkBlue \
			netin		DarkGreen \
			netout		purple \
			separator	black \
			usedmem		black \
			title		black ]
	}

	# Set the theme on the alements of the bar
	proc set_theme { theme } {
		variable currenttheme
		variable baritems
		variable widgetarray

		switch $theme {
			light {
				set currenttheme light
				array set color [light]
			}
			dark {
				set currenttheme dark
				array set color [dark]
			}
			default {
				return
			}
		}

		.bar configure -background $color(background)
		foreach item $baritems {
			array set widget $widgetarray($item)
			switch $widget(type) {
				vseparator {
					.bar.$item configure -background $color(separator)
				}
				default {
					.bar.$item configure -bg $color(background) -fg $color($item)
				}
			}
		}

		devices::set_theme $theme
		maildir::set_theme $theme
		musicpd::set_theme $theme
	}

	# Update mixer after button event
	proc update_mixer {} {
		variable mixer
		variable icons
		set mixer "$icons(mixer) [sysinfo::getmixervol]"
	}

	# Send a message to the window manager
	proc wm_message { wmmessage } {
		variable xscreen
		variable wmport

		if {[catch {set channel [socket localhost $wmport]} ]} {
			puts stderr "Could not open socket on port $wmport!\n"
			return
		}
		puts $channel "$xscreen;$wmmessage"
		close $channel
	}

	proc read_socket {channel} {
		variable currenttheme
		variable desktops
		variable mode
		variable wintitle
		variable metar

		if {[gets $channel line] < 0} {
			close $channel
			return
		}
		close $channel

		set length 0
		set line [string trimright $line]
		set equal [string first "=" $line]
		if {$equal == -1} {
			set key $line
			set value ""
		} elseif {$equal > 0} {
			set key [string range $line 0 $equal-1]
			set value [string range $line $equal+1 end]
			set length [tcl::mathfunc::min [string length $value] 110]
		} else {
			return
		}
		if {$key == "window_active"} {
			$wintitle delete 1.0 end
			$wintitle configure -width $length
			$wintitle insert 1.0 $value
			foreach i [$wintitle search -all -regexp {[\u2000-\u28ff\U1f000-\U1faff]} 1.0 end] {
				$wintitle tag add emoji $i
			}
		} elseif {$key == "no_window_active"} {
			$wintitle delete 1.0 end
		} elseif {$key == "desktop_list"} {
			set desktops $value
		} elseif {$key == "desktop_mode"} {
			set mode " $value"
		} elseif {$key == "mixer_volume"} {
			statusbar::update_mixer
		} elseif { $key == "toggle_metar_window" } {
			metar::toggle_window
		} elseif { $key == "update_metar_report" } {
			metar::update
		} elseif {$key == "set_theme"} {
			if {$value != $currenttheme} {
				set_theme $value
				metar::set_theme $value
			}
		} elseif { $key == "musicpd_update" } {
			musicpd::update
		}
	}

	proc server_accept {channel host port} {
		fconfigure $channel -buffering none -blocking 0
		fileevent $channel readable [list statusbar::read_socket $channel]
	}

	# Update the statusbar
	proc update {} {
		variable icons
		variable loadavg
		variable usedmem
		variable netin
		variable netout
		variable datetime
		variable mixer

		set loadavg "[sysinfo::getloadavg] "
		set usedmem "[join [sysinfo::getusedmemswap]] "
		set netstats [sysinfo::getnetstats em0]
		set netin "$icons(down)[join [lindex $netstats 0]]"
		set netout "$icons(up)[join [lindex $netstats 1]]"

		set datetime [clock format [clock seconds] \
			-locale $::env(LANG) -format {%d %b %H:%M }]

		set mixer "$icons(mixer) [sysinfo::getmixervol]"

		devices::update
		maildir::update
		musicpd::update
	}

	# Setup the static items in the statusbar
	proc setup {} {
		variable baritems
		variable currenttheme
		variable themefile
		variable widgetarray
		variable wintitle

		frame .bar
		pack .bar -fill both -expand 1
		foreach item $baritems {
			array set widget $widgetarray($item)
			switch $widget(type) {
				label {
					pack [label .bar.$item -font $widget(font) \
						-text $widget(text)] -side $widget(side)
				}
				text {
					set wintitle [text .bar.$item -font $widget(font) \
						-height $widget(height) -borderwidth 0 \
						-highlightthickness 0 -wrap word]
					$wintitle tag configure emoji -font emoji
					pack $wintitle -side $widget(side) -expand 1
				}
				var {
					pack [label .bar.$item -font $widget(font) \
						-textvar $widget(text)] -side $widget(side)
				}
				vseparator {
					pack [frame .bar.$item -width 1] -fill y -padx 5 \
						-side $widget(side)
				}
			}
		}

		bind .bar.mode <1> { statusbar::wm_message {PrevMode} }
		bind .bar.mode <3> { statusbar::wm_message {NextMode} }
		bind .bar.desktop <MouseWheel> {
			if {%D < 0} {
				statusbar::wm_message {PrevDesktop}
			} else {
				statusbar::wm_message {NextDesktop}
			}
		}
		bind .bar.loadavg <1> { exec xterm +sb -class top -e top & }
		bind .bar.mixer <MouseWheel> {
			if {%D < 0} {
				exec mixer vol=-0.05
			} else {
				exec mixer vol=+0.05
			}
			statusbar::update_mixer
		}
		bind .bar.metar <1> { metar::toggle_window }
		bind .bar.metar <2> { metar::update }

		devices::setup .bar infosep left
		maildir::setup .bar metarsep right
		musicpd::setup .bar mixersep right

		if {[file exists $themefile]} {
			set_theme [string trim [utils::read_file $themefile]]
		} else {
			set_theme light
		}
		metar::setup $currenttheme

		utils::every 2000 statusbar::update
	}
}

set lang fr
set timezone :America/Montreal

tk appname "statusbar"
font create normal -family NotoSans -size 11
font create italic -family NotoSans -size 11 -slant italic
font create bold -family NotoSans -size 11 -weight bold
font create emoji -family NotoColorEmoji -size 11

wm attributes . -topmost 1 -type dock
wm geometry . "[winfo vrootwidth .]x26+0+0"
wm title . "statusbar.tk version 20250125"

statusbar::setup

socket -server statusbar::server_accept -myaddr localhost 9995
vwait forever
