#!/usr/bin/env wish9.0
package require MPD

namespace eval musicpd {
	variable musicpd_active 0
	variable activepopup 0

	namespace export setup update set_theme

	array set icons [ list \
		0	\ueb92 \
		1	\ueb78 \
		2	\uef32 \
		3	\uef82 ]
}

proc musicpd::dark {} {
	set theme [ list \
		background	{#3b4252} \
		musicpd		CadetBlue3 \
		title	 	LightGray \
		separator	OliveDrab ]
}

proc musicpd::day {} {
	set theme [ list \
		background	gray90 \
		musicpd		DarkBlue \
		title	 	black \
		separator	black ]
}

# Set the theme for the devices
proc musicpd::set_theme { currenttheme } {
	variable musicframe
	variable musicsep
	variable theme

	array set theme [$currenttheme]
	$musicframe configure -bg $theme(background) -fg $theme(musicpd)
	$musicsep configure -background $theme(separator)
}

# Show what is currently playing on MPD
proc musicpd::update {} {
	variable musicframe
	variable musicsep
	variable musicpos
	variable musicside
	variable musicpd_active
	variable activepopup
	variable icons

	set state [mpd::state]

	if { $state == 2 || $state == 3 } {
		$musicframe configure -text $icons($state)
		if {!$musicpd_active} {
			pack $musicframe -after $musicpos -side $musicside
			pack $musicsep -after $musicframe -fill y -padx 5 -side $musicside
			set musicpd_active 1
		}

	} else {
		if {$musicpd_active} {
			pack forget $musicframe $musicsep
			set musicpd_active 0
		}
	}
	if { $musicpd_active && $activepopup } {
		update_info
	}
}

# Setup widget for MPD
proc musicpd::setup { widget position side } {
	variable musicframe
	variable musicsep
	variable musicpos
	variable musicside

	set musicframe $widget.musicpd
	set musicsep $widget.musicsep
	set musicpos $widget.$position
	set musicside $side

	label $musicframe -font normal
	frame $musicsep -width 1

	bind $musicframe <Enter> {
		musicpd::show_info
	}
	bind $musicframe <Leave> {
		destroy .musicpopup
		set musicpd::activepopup 0
	}
	bind $musicframe <1> {
		if {![catch {exec mpc toggle}]} { musicpd::update }
	}
	bind $musicframe <2> {
		exec xterm +sb -class ncmpcpp -e ncmpcpp &
	}
	bind $musicframe <3> {
		if {![catch {exec mpc stop}]} { musicpd::update }
	}
	bind $musicframe <MouseWheel> {
		if {%D < 0} {
			if {![catch {exec mpc next}]} { musicpd::update_info }
		} else {
			if {![catch {exec mpc prev}]} { musicpd::update_info }
		}
	}
	mpd::connect $::env(MPD_HOST)
}

# Show info on current track
proc musicpd::show_info { } {
	variable theme
	variable activepopup
	variable musictext

	set activepopup 1
	toplevel .musicpopup -background $theme(background) -class NowPlaying
	wm title .musicpopup "Now Playing"
	wm attributes .musicpopup -type dialog
	wm overrideredirect .musicpopup 1
	set xpos [tcl::mathfunc::max [expr [winfo pointerx .] - 120] 1]
	wm geometry .musicpopup +$xpos+27

	set musictext [text .musicpopup.text -font normal -height 2 \
			-borderwidth 1 -highlightthickness 0 ]
	pack $musictext -side left -padx 5 -pady 3
	update_info
}

# Update info on current track
proc musicpd::update_info { } {
	variable theme
	variable musictext
	set mpdtitle [join [mpd::currenttitle]]

	set width 0
	foreach line [split $mpdtitle \n] {
		set width [tcl::mathfunc::max [string length $line] $width]
	}
	$musictext delete 1.0 end
	$musictext insert 1.0 $mpdtitle
	$musictext configure -width $width -bg $theme(background) -fg $theme(title)
}

package provide musicpd 0.4
