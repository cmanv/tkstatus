#!/usr/bin/env wish9.0
package require MPD

namespace eval musicpd {
	variable musicpd_active 0
	variable now_playing ""

	namespace export setup update set_theme

	proc dark {} {
		set theme [ list \
			background	{#3b4252} \
			musicpd		CadetBlue3 \
			separator	OliveDrab ]
	}

	proc light {} {
		set theme [ list \
			background	gray90 \
			musicpd		DarkBlue \
			separator	black ]
	}

	# Set the theme for the devices
	proc set_theme { currenttheme } {
		variable musicframe
		variable musicsep

		array set theme [$currenttheme]
		$musicframe configure -bg $theme(background) -fg $theme(musicpd)
		$musicsep configure -background $theme(separator)
	}

	# Show what is currently playing on MPD
	proc update {} {
		variable musicframe
		variable musicsep
		variable musicpos
		variable musicside
		variable musicpd_active
		variable now_playing

		set now_playing [join [mpd::currenttitle]]
		if {[string length $now_playing]} {
			if {!$musicpd_active} {
				pack $musicframe -after $musicpos -side $musicside
				pack $musicsep -after $musicframe -fill y -padx 5 -side $musicside
				set musicpd_active 1
			}

		} else {
			if {$musicpd_active} {
				pack forget $musicframe $musicsep
				set musicpd_active 0
			}
		}
	}

	# Setup widget for MPD
	proc setup { widget position side } {
		variable musicframe
		variable musicsep
		variable musicpos
		variable musicside

		set musicframe $widget.musicpd
		set musicsep $widget.musicsep
		set musicpos $widget.$position
		set musicside $side

		label $musicframe -font italic -textvar musicpd::now_playing
		frame $musicsep -width 1

		bind $musicframe <1> {
			if {![catch {exec mpc toggle}]} { musicpd::update }
		}
		bind $musicframe <2> {
			exec xterm +sb -class ncmpcpp -e ncmpcpp &
		}
		bind $musicframe <3> {
			if {![catch {exec mpc stop}]} { musicpd::update }
		}
		bind $musicframe <MouseWheel> {
			if {%D < 0} {
				if {![catch {exec mpc next}]} { musicpd::update }
			} else {
				if {![catch {exec mpc prev}]} { musicpd::update }
			}

		}
		mpd::connect $::env(MPD_HOST)
	}
}

package provide musicpd 0.3
