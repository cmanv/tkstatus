package require Tk 9.0
package require fileutil
package require zstatus::utils
package require zstatus::metar::decode

namespace eval zstatus::metar {
	font create m_italic -family Sans -size 12 -slant italic
	font create m_normal -family Sans -size 12
	font create m_large -family Sans -size 13
	font create m_icon -family Remix -size 16

	set metar_locales {C fr}
	array set weather_label {C "Weather conditions:" fr "Conditions météorologiques :"}
	array set station_label {C "Station:" fr "Station :"}
	array set issued_label {C "Issued on:" fr "Émis le :"}
	array set status_label {C "Status:" fr "Statut :"}
	array set wind_label {C "Wind:" fr "Vent :"}
	array set gust_label {C "Gust:" fr "Rafale :"}
	array set dew_label {C "Dew point:" fr "Point de rosée :"}
	array set rhumidity_label {C "Relative humidity:" fr "Humidité relative :"}
	array set pressure_label {C "Pressure:" fr "Pression :"}
	array set visibility_label {C "Visibility:" fr "Visibilité :"}
	array set clouds_label {C "Clouds:" fr "Nuages :"}
	array set precips_label {C "Precipitations:" fr "Précipitations :"}

	array set header1 { font m_large light black dark gray80 }
	array set header2 { font m_italic light black dark gray80 }
	array set value1 { font m_normal light DarkBlue dark CadetBlue3 }
	array set value2 { font m_normal light DarkGreen dark PaleGreen3 }
	array set icons { font m_icon light orange dark Gold }

	array set station {}
	variable  state_visible 0
	namespace export setup update set_theme show_tooltip hide_tooltip
}

proc zstatus::metar::show_tooltip {} {
	variable statusbar
	variable metar
	variable header1
	variable theme

	set window [toplevel .metartooltip -highlightthickness 0\
			-background $statusbar($theme)]
	wm title $window "Metar Summary"
	wm attributes $window -type dialog
	wm overrideredirect $window 1

	set width [string length zstatus::metar::report(tooltip)]
	pack [label $window.text -font $metar(font) -width $width\
		-fg $header1($theme) -bg $statusbar($theme)\
		-textvar zstatus::metar::report(tooltip)]\
		-side left -padx 1 -pady 3

	bind $window <Map> { zstatus::map_window .metartooltip }
}

proc zstatus::metar::hide_tooltip {} {
	destroy .metartooltip
}

proc zstatus::metar::toggle_window {} {
	variable station
	variable metarwin
	if ![info exists station(code)] { return }

	variable state_visible
	if {$state_visible} {
		wm state $metarwin withdrawn
		set state_visible 0
	} else {
		wm state $metarwin normal
		raise $metarwin
		set state_visible 1
	}
}

proc zstatus::metar::set_theme_header { header } {
	variable statusbar
	variable header2
	variable value1
	variable theme

	$header configure -background $statusbar($theme)
	$header.keys configure -background $statusbar($theme)
	$header.keys.station configure -background $statusbar($theme)
	$header.keys.station.text configure -bg $statusbar($theme) -fg $header2($theme)
	$header.keys.date configure -background $statusbar($theme)
	$header.keys.date.text configure -bg $statusbar($theme) -fg $header2($theme)
	$header.keys.status configure -background $statusbar($theme)
	$header.keys.status.text configure -bg $statusbar($theme) -fg $header2($theme)
	$header.values configure -background $statusbar($theme)
	$header.values.station configure -background $statusbar($theme)
	$header.values.station.text configure -bg $statusbar($theme) -fg $value1($theme)
	$header.values.date configure -background $statusbar($theme)
	$header.values.date.text configure -bg $statusbar($theme) -fg $value1($theme)
	$header.values.status configure -background $statusbar($theme)
	$header.values.status.text configure -bg $statusbar($theme) -fg $value1($theme)
}

proc zstatus::metar::set_theme_summary { summary } {
	variable statusbar
	variable separator
	variable header2
	variable value1
	variable value2
	variable icons
	variable theme

	$summary configure -background $statusbar($theme)
	$summary.temp configure -background $statusbar($theme)
	$summary.temp.status configure -background $statusbar($theme)
	$summary.temp.status.icon configure -bg $statusbar($theme) -fg $value2($theme)
	$summary.temp.status.text configure -bg $statusbar($theme) -fg $value2($theme)
	$summary.temp.remark configure -background $statusbar($theme)
	$summary.temp.remark.key configure -bg $statusbar($theme) -fg $header2($theme)
	$summary.temp.remark.value configure -bg $statusbar($theme) -fg $value2($theme)
	$summary.separator configure -background $separator($theme)
	$summary.time configure -background $statusbar($theme)
	$summary.time.sunrise configure -background $statusbar($theme)
	$summary.time.sunrise.icon configure -bg $statusbar($theme) -fg $icons($theme)
	$summary.time.sunrise.arrow configure -bg $statusbar($theme) -fg $value1($theme)
	$summary.time.sunrise.hour configure -bg $statusbar($theme) -fg $value1($theme)
	$summary.time.sunset configure -background $statusbar($theme)
	$summary.time.sunset.icon configure -bg $statusbar($theme) -fg $icons($theme)
	$summary.time.sunset.arrow configure -bg $statusbar($theme) -fg $value1($theme)
	$summary.time.sunset.hour configure -bg $statusbar($theme) -fg $value1($theme)
}

proc zstatus::metar::set_theme_grid { grid } {
	variable statusbar
	variable header2
	variable value2
	variable theme

	$grid configure -background $statusbar($theme)
	$grid.wind configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.wind_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.gust configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.gust_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.dew configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.dew_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.rhumidity configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.rhumidity_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.pressure configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.pressure_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.visibility configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.visibility_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.clouds configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.clouds_val configure -bg $statusbar($theme) -fg $value2($theme)
	$grid.precips configure -bg $statusbar($theme) -fg $header2($theme)
	$grid.precips_val configure -bg $statusbar($theme) -fg $value2($theme)
}

proc zstatus::metar::set_theme {newtheme} {
	variable station
	if ![info exists station(code)] { return }

	variable statusbar
	variable metarwin
	variable header1
	variable theme

	set theme $newtheme
	$metarwin configure -background $statusbar($theme)
	set_theme_header $metarwin.header
	set_theme_summary $metarwin.summary
	$metarwin.title2 configure -bg $statusbar($theme) -fg $header1($theme)
	set_theme_grid $metarwin.grid
}

proc zstatus::metar::update_grid { grid } {
	variable report

	if {![string length $report(wind)]} {
		grid remove $grid.wind $grid.wind_val
	} else {
		grid $grid.wind $grid.wind_val
	}

	if {![string length $report(gust)]} {
		grid remove $grid.gust $grid.gust_val
	} else {
		grid $grid.gust $grid.gust_val
	}

	if {![string length $report(visibility)]} {
		grid remove $grid.visibility $grid.visibility_val
	} else {
		grid $grid.visibility $grid.visibility_val
	}

	if {![string length $report(pressure)]} {
		grid remove $grid.pressure $grid.pressure_val
	} else {
		grid $grid.pressure $grid.pressure_val
	}

	$grid.clouds_val delete 1.0 end
	if {[info exists report(clouds)]} {
		if {![string length $report(clouds)]} {
			grid remove $grid.clouds $grid.clouds_val
		} else {
			set width 0
			set lines [split $report(clouds) \n]
			set height [llength $lines]
			foreach line $lines {
				set width [tcl::mathfunc::max [string length $line] $width]
			}
			incr width
			$grid.clouds_val insert 1.0 $report(clouds)
			$grid.clouds_val configure -height $height -width $width
			grid $grid.clouds $grid.clouds_val
		}
	}

	$grid.precips_val delete 1.0 end
	if {[info exists report(precips)]} {
		if {![string length $report(precips)]} {
			grid remove $grid.precips $grid.precips_val
		} else {
			set width 0
			set lines [split $report(precips) \n]
			set height [llength $lines]
			foreach line $lines {
				set width [tcl::mathfunc::max [string length $line] $width]
			}
			incr width
			$grid.precips_val insert 1.0 $report(precips)
			$grid.precips_val configure -height $height -width $width
			grid $grid.precips $grid.precips_val
		}
	}
}

proc zstatus::metar::update {} {
	variable metar
	variable metarwin
	variable station
	variable report
	variable locale

	if ![info exists station(code)] {
		set status [decode::fetch_station_info $metar(station)]
		if {$status == {KO}} {
			set report(statusbar) "<?>"
			return
		}
	}


	array set station [decode::update_station]
	array set report [decode::get_report $locale]

	update_grid $metarwin.grid
}

proc zstatus::metar::setup_header { header } {
	variable theme
	variable header2
	variable value1
	variable report
	variable locale
	variable station_label
	variable issued_label
	variable status_label

	pack [frame $header] -anchor w -padx 15 -pady 5 -side top
	pack [frame $header.keys] -side left
	pack [frame $header.keys.station] -side top -anchor w
	pack [label $header.keys.station.text -font $header2(font)\
			-text $station_label($locale) ]
	pack [frame $header.keys.date]  -side top -anchor w
	pack [label $header.keys.date.text -font $header2(font)\
			-text $issued_label($locale)]
	pack [frame $header.keys.status] -side top -anchor w
	pack [label $header.keys.status.text -font $header2(font)\
			-text $status_label($locale)]
	pack [frame $header.values] -padx 10 -side left -anchor w
	pack [frame $header.values.station] -side top -anchor w
	pack [label $header.values.station.text -font $value1(font)\
		-textvar zstatus::metar::station(site)]
	pack [frame $header.values.date] -side top -anchor w
	pack [label $header.values.date.text -font $value1(font)\
		-textvar zstatus::metar::report(date)]
	pack [frame $header.values.status] -side top -anchor w
	pack [label $header.values.status.text -font $value1(font)\
		-textvar zstatus::metar::report(request_message)]
}

proc zstatus::metar::setup_summary { summary } {
	variable theme
	variable header1
	variable header2
	variable value1
	variable value2
	variable icons
	variable report
	variable station

	pack [frame $summary] -anchor w -padx 10 -pady 5 -side top
	pack [frame $summary.temp] -side left
	pack [frame $summary.temp.status] -side top -anchor w
	pack [label $summary.temp.status.icon -font $icons(font)\
		-textvar zstatus::metar::report(weather_icon) ] -side left
	pack [label $summary.temp.status.text -font $header1(font)\
		-textvar zstatus::metar::report(summary) ] -side left -padx 10
	pack [frame $summary.temp.remark] -side top -anchor w
	pack [label $summary.temp.remark.key -font $header2(font)\
		-textvar zstatus::metar::report(note) ] -side left
	pack [label $summary.temp.remark.value -font $value2(font)\
		-textvar zstatus::metar::report(note_val) ] -side left -padx 5
	pack [frame $summary.separator -width 1] -padx 15 -fill y -side left
	pack [frame $summary.time] -anchor n -side left
	pack [frame $summary.time.sunrise] -anchor w -side top -pady 5 
	pack [label $summary.time.sunrise.icon -font $value1(font)\
		 -text $::remixicons(sunrise)] -side left
	pack [label $summary.time.sunrise.arrow -font $value1(font)\
		-text $::remixicons(up)] -side left
	pack [label $summary.time.sunrise.hour -font $value1(font)\
		 -textvar zstatus::metar::station(sunrise)] -side left
	pack [frame $summary.time.sunset] -anchor w -side top -pady 5
	pack [label $summary.time.sunset.icon -font $value1(font)\
		 -text $::remixicons(sunset)] -side left
	pack [label $summary.time.sunset.arrow -font $value1(font)\
		-text $::remixicons(down)] -side left
	pack [label $summary.time.sunset.hour -font $value1(font)\
		-textvar zstatus::metar::station(sunset)] -side left
}

proc zstatus::metar::setup_info_grid { grid } {
	variable theme
	variable header2
	variable value2
	variable report
	variable locale
	variable wind_label
	variable gust_label
	variable dew_label
	variable rhumidity_label
	variable pressure_label
	variable visibility_label
	variable clouds_label
	variable precips_label

	pack [frame $grid] -anchor w -padx 15 -side top
	grid columnconfigure $grid 1 -weight 1

	label $grid.wind -font $header2(font) -text $wind_label($locale)
	label $grid.wind_val -font $value2(font) -textvar zstatus::metar::report(wind)
	set row 0
	grid configure $grid.wind -row $row -column 0 -sticky w
	grid configure $grid.wind_val -row $row -column 1 -sticky w

	label $grid.gust -font $header2(font) -text $gust_label($locale)
	label $grid.gust_val -font $value2(font) -textvar zstatus::metar::report(gust)
	incr row
	grid configure $grid.gust -row $row -column 0 -sticky w
	grid configure $grid.gust_val -row $row -column 1 -sticky w

	label $grid.dew -font $header2(font) -text $dew_label($locale)
	label $grid.dew_val -font $value2(font) -textvar zstatus::metar::report(dew)
	incr row
	grid configure $grid.dew -row $row -column 0 -sticky w
	grid configure $grid.dew_val -row $row -column 1 -sticky w

	label $grid.rhumidity -font $header2(font) -text $rhumidity_label($locale)
	label $grid.rhumidity_val -font $value2(font) \
		-textvar zstatus::metar::report(rel_humidity)
	incr row
	grid configure $grid.rhumidity -row $row -column 0 -sticky w
	grid configure $grid.rhumidity_val -row $row -column 1 -sticky w

	label $grid.pressure -font $header2(font) -text $pressure_label($locale)
	label $grid.pressure_val -font $value2(font) \
		-textvar zstatus::metar::report(pressure)
	incr row
	grid configure $grid.pressure -row $row -column 0 -sticky w
	grid configure $grid.pressure_val -row $row -column 1 -sticky w

	label $grid.visibility -font $header2(font) -text $visibility_label($locale)
	label $grid.visibility_val -font $value2(font) \
		-textvar zstatus::metar::report(visibility)
	incr row
	grid configure $grid.visibility -row $row -column 0 -sticky w
	grid configure $grid.visibility_val -row $row -column 1 -sticky w

	label $grid.clouds -font $header2(font) -text $clouds_label($locale)
	text $grid.clouds_val -font $value2(font) -borderwidth 0 -highlightthickness 0\
			 -wrap none
	incr row
	grid configure $grid.clouds -row $row -column 0 -sticky nw
	grid configure $grid.clouds_val -row $row -column 1 -sticky w

	label $grid.precips -font $header2(font) -text $precips_label($locale)
	text $grid.precips_val -font $value2(font) -borderwidth 0 -highlightthickness 0\
			 -wrap none
	incr row
	grid configure $grid.precips -row $row -column 0 -sticky nw
	grid configure $grid.precips_val -row $row -column 1 -sticky w
}

proc zstatus::metar::setup {} {
	variable metar_locales
	variable locale
	variable theme
	variable metar
	variable metarwin
	variable weather_label
	variable header1

	array set metar $::widgetarray(metar)

	if ![info exists metar(station)] {
		variable report
		set report(statusbar) "-"
		return
	}

	variable statusbar
	variable separator
	array set statusbar $::widgetarray(statusbar)
	array set separator $::widgetarray(separator)

	set index [lsearch $metar_locales [lindex [split $::config(lang) "_"] 0]]
	if {$index < 0} {
		set locale C
	} else {
		set locale [lindex $metar_locales $index]
	}

	set metarwin [toplevel .metarreport -class Metar -bd 1 -relief solid]
	setup_header $metarwin.header
	setup_summary $metarwin.summary
	pack [label $metarwin.title2 -font $header1(font)\
		-text $weather_label($locale)] -side top -anchor w -pady 5 -padx 15
	setup_info_grid $metarwin.grid

	wm attributes $metarwin -type dialog
	wm overrideredirect $metarwin 1
	wm resizable $metarwin 0 0
	wm title $metarwin {Metar Report}
	wm state $metarwin withdrawn

	bind $metarwin <3> { zstatus::metar::toggle_window }
	bind $metarwin <Map> { zstatus::map_window .metarreport }
	set delay [expr $metar(delay) * 60000]
	zstatus::utils::every $delay zstatus::metar::update
}
package provide @PACKAGE_NAME@ @PACKAGE_VERSION@
