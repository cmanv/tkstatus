#!/usr/bin/env wish9.0

namespace eval devices {
	array set icons [ list	printer	\uefd2	sdcard	\uf066	udisk	\uf1cc ]

	namespace export setup update set_theme

	proc dark {} {
		set theme [ list \
			background	{#3b4252} \
			devices		PaleGreen3 \
			separator	OliveDrab ]
	}

	proc light {} {
		set theme [ list \
			background	gray90 \
			devices		DarkGreen \
			separator	black ]
	}

	# Set the theme for musicpd
	proc set_theme { currenttheme } {
		variable devicelist
		variable devframe
		variable devsep

		array set theme [$currenttheme]
		$devframe configure -background $theme(background)
		$devsep configure -background $theme(separator)
		foreach device $devicelist {
			$devframe.$device configure -bg $theme(background) -fg $theme(devices)
		}
	}

	# Check the presence of removable devices.
	proc update {} {
		variable devicelist
		variable devicearray
		variable devframe
		variable devsep
		variable devpos
		variable devside

		foreach devid $devicelist {
			array set device $devicearray($devid)
			if {[file exists $device(path)]} {
				if {!$device(visible)} {
					if {![string length [pack slaves $devframe]]} {
						pack $devframe -after $devpos -side $devside
						pack $devsep -after $devframe -fill y \
							-padx 5 -side $devside
					}
					pack $devframe.$devid -in $devframe -side left
					set device(visible) 1
					set devicearray($devid) [array get device]
				}
			} else {
				if {$device(visible)} {
					pack forget $devframe.$devid
					if {![string length [pack slaves $devframe]]} {
						pack forget $devframe $devsep
					}
					set device(visible) 0
					set devicearray($devid) [array get device]
				}
			}
		}

	}

	# Setup the removable devices
	proc setup { widget position side } {
		variable devicelist
		variable devicearray
		variable icons
		variable devframe
		variable devsep
		variable devpos
		variable devside

		set devframe $widget.devices
		set devsep $widget.devivessep
		set devpos $widget.$position
		set devside $side

		frame $devframe
		frame $devsep -width 1

		set devicelist [ list da0 da1 mmcsd0 ulpt0 ]
		set deviceicons [ list $icons(udisk) $icons(udisk) \
				$icons(sdcard) $icons(printer) ]

		array set devicearray {}
		foreach devid $devicelist icon $deviceicons {
			array set device {}
			set device(path) "/dev/$devid"
			set device(visible) 0
			set devicearray($devid) [array get device]

			label $devframe.$devid
			$devframe.$devid configure -font normal -text "$icon $devid"
		}
	}

}

package provide devices 0.1
