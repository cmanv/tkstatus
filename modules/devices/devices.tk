#!/usr/bin/env wish9.0
namespace eval devices {
	array set icons { printer \uefd2 sdcard \uf066 udisk \uf1cc }
	namespace export setup update set_theme
}

# Set the theme for musicpd
proc devices::set_theme {theme} {
	variable devicelist
	variable devframe
	variable devsep

	array set devices $::widgetarray(devices)
	array set statusbar $::widgetarray(statusbar)
	array set separator $::widgetarray(separator)

	$devframe configure -background $statusbar($theme)
	$devsep configure -background $separator($theme)
	foreach device $devicelist {
		$devframe.$device configure -fg $devices($theme) -bg $statusbar($theme)
	}
}

# Check the presence of removable devices.
proc devices::update {} {
	variable devicelist
	variable devicearray
	variable devframe
	variable devsep
	variable devpos
	variable devside

	foreach devid $devicelist {
		array set device $devicearray($devid)
		if [file exists $device(path)] {
			if {!$device(visible)} {
				if {![string length [pack slaves $devframe]]} {
					pack $devframe -after $devpos -side $devside
					pack $devsep -after $devframe -fill y \
						-padx 5 -side $devside
				}
				pack $devframe.$devid -in $devframe -side left
				set device(visible) 1
				set devicearray($devid) [array get device]
			}
		} elseif {$device(visible)} {
			pack forget $devframe.$devid
			if {![string length [pack slaves $devframe]]} {
				pack forget $devframe $devsep
			}
			set device(visible) 0
			set devicearray($devid) [array get device]
		}
	}
}

# Setup the removable devices
proc devices::setup { base position side } {
	variable devicelist
	variable devicearray
	variable icons
	variable devframe
	variable devsep
	variable devpos
	variable devside

	set devframe $base.devices
	set devsep $base.devivessep
	set devpos $base.$position
	set devside $side

	frame $devframe
	frame $devsep -width 1

	set devicelist [ list da0 da1 mmcsd0 ulpt0 ]
	set deviceicons [ list $icons(udisk) $icons(udisk) \
			$icons(sdcard) $icons(printer) ]

	array set devices $::widgetarray(devices)
	array set devicearray {}
	foreach devid $devicelist icon $deviceicons {
		array set device {}
		set device(path) "/dev/$devid"
		set device(visible) 0
		set devicearray($devid) [array get device]

		label $devframe.$devid
		$devframe.$devid configure -font $devices(font) -text "$icon $devid"
	}
}

package provide devices @PACKAGE_VERSION@
